buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.12.0"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.40.0"
}

group 'com.github.qwazer'
version "$VERSION"
description 'A plugin for publishing markdown or confluence wiki files to to an Atlassian Confluence server'

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.gradle.plugin-publish'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    implementation gradleApi()
    implementation "org.slf4j:slf4j-api:1.7.25"
    implementation "org.springframework:spring-core:$SPRING_VERSION"
    implementation "org.springframework:spring-context:$SPRING_VERSION"
    implementation "org.springframework:spring-web:$SPRING_VERSION"
    implementation "com.jayway.jsonpath:json-path:2.4.0"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.3"
    implementation "org.jsoup:jsoup:1.11.2"
    implementation "org.pegdown:pegdown:1.6.0"


    testImplementation gradleTestKit()
    testImplementation "commons-io:commons-io:2.6"
    testImplementation "org.junit.jupiter:junit-jupiter:$JUNIT_VERSION"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$JUNIT_VERSION"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.mockito:mockito-core:2.18.0"
    testImplementation "org.springframework:spring-test:$SPRING_VERSION"
}

test {
    useJUnitPlatform()
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = project.hasProperty('bintrayRepo') ? project.property('bintrayRepo') : System.getenv('BINTRAY_REPO')
        name = 'markdown-confluence-gradle-plugin'
        userOrg = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/qwazer/markdown-confluence-gradle-plugin.git'
        websiteUrl = 'https://github.com/qwazer/markdown-confluence-gradle-plugin'
        issueTrackerUrl = 'https://github.com/qwazer/markdown-confluence-gradle-plugin/issues'
        labels = ['markdown','confluence']
        version {
            name = project.version
        }
    }
}



publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.github.qwazer'
            artifactId project.name
            version project.version
        }
    }
}

pluginBundle {
    website = 'https://github.com/qwazer/markdown-confluence-gradle-plugin'
    vcsUrl = 'https://github.com/qwazer/markdown-confluence-gradle-plugin.git'
    description = project.description
    tags = ['markdown', 'confluence']

    plugins {
        markdownConfluenceGradlePlugin {
            id = 'com.github.qwazer.markdown-confluence'
            displayName = 'Markdown Confluence'
        }
    }
}