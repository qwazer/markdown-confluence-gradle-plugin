buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.1"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

group 'com.github.qwazer'
version "$VERSION"
description 'A plugin for publishing markdown or confluence wiki files to to an Atlassian Confluence server'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.gradle.plugin-publish'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}


dependencies {
    compile gradleApi()
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "org.springframework:spring-core:$SPRING_VERSION"
    compile "org.springframework:spring-context:$SPRING_VERSION"
    compile "org.springframework:spring-web:$SPRING_VERSION"
    compile "com.jayway.jsonpath:json-path:2.4.0"
    compile "org.jsoup:jsoup:1.11.2"
    compile "org.pegdown:pegdown:1.6.0"


    testCompile gradleTestKit()
    testCompile "commons-io:commons-io:2.6"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.18.0"
    testCompile "org.springframework:spring-test:$SPRING_VERSION"
}


bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = project.hasProperty('bintrayRepo') ? project.property('bintrayRepo') : System.getenv('BINTRAY_REPO')
        name = 'markdown-confluence-gradle-plugin'
        userOrg = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/qwazer/markdown-confluence-gradle-plugin.git'
        websiteUrl = 'https://github.com/qwazer/markdown-confluence-gradle-plugin'
        issueTrackerUrl = 'https://github.com/qwazer/markdown-confluence-gradle-plugin/issues'
        labels = ['markdown','confluence']
        version {
            name = project.version
        }
    }
}



publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.github.qwazer'
            artifactId project.name
            version project.version
        }
    }
}

pluginBundle {
    website = 'https://github.com/qwazer/markdown-confluence-gradle-plugin'
    vcsUrl = 'https://github.com/qwazer/markdown-confluence-gradle-plugin.git'
    description = project.description
    tags = ['markdown', 'confluence']

    plugins {
        markdownConfluenceGradlePlugin {
            id = 'com.github.qwazer.markdown-confluence'
            displayName = 'Markdown Confluence'
        }
    }
}