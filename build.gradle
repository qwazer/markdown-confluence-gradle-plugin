plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'jvm-test-suite'
    id 'com.gradle.plugin-publish' version '1.2.0'
    id 'com.github.ben-manes.versions' version '0.40.0'
}

group 'com.github.qwazer'
description 'A plugin for publishing markdown or confluence wiki files to an Atlassian Confluence server'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
}

dependencies {
    api 'com.squareup.okhttp3:okhttp:4.12.0'
    api 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    api 'commons-codec:commons-codec:1.16.0'
    api 'org.bsc.maven:confluence-markdown-processor-commonmark:7.13'
    api 'org.bsc.maven:confluence-markdown-processor-pegdown:6.20'
}

testing {
    suites {
        withType(JvmTestSuite).matching { it.name in ['test', 'integrationTest'] }.configureEach {
            useJUnit()
            dependencies {
                implementation 'org.mockito:mockito-core:4.11.0'
                implementation 'com.squareup.okhttp3:mockwebserver:4.11.0'
            }
        }

        integrationTest(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST
            dependencies {
                implementation project()
            }
        }

        functionalTest(JvmTestSuite) {
            testType = TestSuiteType.FUNCTIONAL_TEST
            useSpock()
            dependencies {
                implementation gradleTestKit()
                implementation project()
            }
        }
    }
}

gradlePlugin {
    website = 'https://github.com/qwazer/markdown-confluence-gradle-plugin'
    vcsUrl = 'https://github.com/qwazer/markdown-confluence-gradle-plugin.git'
    plugins {
        markdownConfluenceGradlePlugin {
            id = 'com.github.qwazer.markdown-confluence'
            implementationClass = 'com.github.qwazer.markdown.confluence.gradle.plugin.ConfluenceGradlePlugin'
            description = project.description
            displayName = 'Markdown Confluence'
            tags.set(['markdown', 'confluence'])
        }
    }
    testSourceSets(testing.suites.functionalTest.sources)
}